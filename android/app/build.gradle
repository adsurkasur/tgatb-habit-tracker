apply plugin: 'com.android.application'

android {
    namespace "com.tgatb.habittracker"
    compileSdk rootProject.ext.compileSdkVersion
    defaultConfig {
    applicationId "com.tgatb.habittracker"
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionCode 700900
    versionName "0.7.0.0"
        ndk {
            debugSymbolLevel 'FULL'
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        aaptOptions {
            // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
            // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
    }
    compileOptions {
    // Compile with Java 21
    sourceCompatibility JavaVersion.VERSION_21
    targetCompatibility JavaVersion.VERSION_21
    }
        signingConfigs {
        release {
            // This checks for the properties being passed by your GitHub Actions workflow
            if (project.hasProperty('androidKeystorePassword')) {
                storeFile file(project.properties['storeFile'])
                storePassword project.properties['androidKeystorePassword']
                keyAlias project.properties['keyAlias']
                keyPassword project.properties['androidKeyPassword']
            }
        }
    }
        buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

// Pin firebase-auth to the version expected by @capacitor-firebase/authentication.
// BoM 34.1.0 resolves firebase-auth to 24.0.1, but the Capacitor plugin (v7.5.x)
// was compiled against firebase-auth 23.1.0. Running 24.x at runtime causes a
// silent failure (error "0:") during Google Sign-In because the major-version
// jump changed internal APIs the plugin depends on.
// SAFE TO REMOVE once the Capacitor Firebase plugin ships a version that
// explicitly supports firebase-auth 24.x.
configurations.all {
    resolutionStrategy.force 'com.google.firebase:firebase-auth:23.1.0'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
    implementation project(':capacitor-android')
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"

    // Firebase BoM and Analytics SDK
    // NOTE: BoM 34.1.0 manages firebase-auth:24.0.1, but we force 23.1.0 above.
    // All other Firebase libs (analytics, etc.) still resolve via the BoM normally.
    implementation platform('com.google.firebase:firebase-bom:34.1.0')
    implementation 'com.google.firebase:firebase-analytics'
    // Add other Firebase SDKs as needed
    // Facebook Android SDK for Firebase Authentication Facebook provider
    implementation 'com.facebook.android:facebook-android-sdk:latest.release'
}

apply from: 'capacitor.build.gradle'

apply plugin: 'com.google.gms.google-services'
